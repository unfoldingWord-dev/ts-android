apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
    }
}

apply plugin: 'com.getkeepsafe.dexcount'

android {
    signingConfigs {
        if (System.getenv("TRAVIS")) {
            release {
                storeFile rootProject.file('translationstudio.keystore')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "com.translationstudio.androidapp"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 149
        versionName "11.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    } 
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-rules.pro')
            if (System.getenv("TRAVIS")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile('proguard-rules.pro')
        }
        applicationVariants.all { variant ->
            if(variant.buildType.name == "release") {
                variant.outputs.each { output ->
                    def newName = "release.apk";
                    def outDir = new File('app/release/');
                    outDir.mkdirs();
                    output.outputFile = new File(outDir, newName);
                }
            }
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }
}

task generateReleaseHtml << {
    println('Preparing release docs');
    def index = new File('app/release/index.html');
    def html  = '<head><link rel="stylesheet" href="style.css">' +
            '<meta name="viewport" content="width=device-width, initial-scale=1.0">' +
            '</head>';
    html += '<h1>tS Android build #<span id="build-num">' + android.defaultConfig.versionCode + '</span> (v' + android.defaultConfig.versionName + ')</h1><ul>';
    if(System.env.TRAVIS_COMMIT) {
        def branch = System.env.TRAVIS_BRANCH;
        def commit = System.env.TRAVIS_COMMIT;
        def buildNumber = System.env.TRAVIS_BUILD_NUMBER;
        def buildId = System.env.TRAVIS_BUILD_ID;
        def repoSlug = System.env.TRAVIS_REPO_SLUG;
        html += '<h2><a href="https://github.com/' + repoSlug + '/commit/' + commit + '" target="_blank">Commit ' + commit.substring(0, 7) + ' on ' + branch + '</a></h2>';
        html += '<h2><a href="https://travis-ci.org/' + repoSlug + '/builds/' + buildId + '" target="_blank">Travis build #' + buildNumber + '</a></h2>';
    } else {
        println('Run on travis for complete output.');
    }
    html += '<li class="ok">Android <span class="status">success</span> <a href="release.apk" class="build-link" data-os="android">Download</a></li>';
    index.text = html;
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
            testLogging {
                // Custom configuration
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'

    compile 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.+'
    compile 'com.jcraft:jsch:0.1.51'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.jpardogo.materialtabstrip:library:1.0.8'
    compile 'com.github.filippudak.progresspieview:library:1.0.4'
    compile 'org.apmem.tools:layouts:1.10'
    compile 'com.itextpdf:itextpdf:5.5.7'
    compile 'com.facebook.rebound:rebound:0.3.8'

    compile 'org.unfoldingword.tools:gogs-client:1.6.0'
    compile 'org.unfoldingword.tools:task-manager:1.5.0'
    compile 'org.unfoldingword.tools:door43-client:0.8.8'
    compile 'org.unfoldingword.tools:http:2.3.0'
    compile 'org.unfoldingword.tools:logger:2.0.0'

    compile project(':html-textview')
    compile project(':seekbarhint')
    compile 'com.google.android.gms:play-services-appindexing:8.1.0'

    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        // Necessary to avoid version conflicts
//        exclude group: 'com.android.support', module: 'appcompat'
//        exclude group: 'com.android.support', module: 'support-v4'
//        exclude group: 'com.android.support', module: 'support-annotations'
//        exclude module: 'recyclerview-v7'
    }
//    // Optional -- UI testing with UI Automator
//    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    testCompile 'junit:junit:4.12'
}